module entity-extraction/generate-entity-files

imports
	libstratego-lib
	libwebdsl-generator
  	libwebdsl-front
  	util
  	lib/-
  	
rules //builder
	
  generate-entity-files: (selected, position, ast, path, project-path) -> None()
  	where	dirname := "entities_generated"
  			; <create-dir-in-workspace>  dirname 
  			; entities := <all-keys-EntDecl
  				; map(EntDecl; complete-entity-with-extends-properties)> 
  			; filteredEntities := <filter(not(is-builtin-entity))> entities
  			; cleanedEntities := <map(try(remove-general-super-from-entity))> filteredEntities
  			; <map(entity-to-file(|dirname))> cleanedEntities
  			; <refresh-workspace-file> $[[project-path]/[dirname]]  
  	
rules // file stuff  
			      
  make-valid-page-from-entity(|moduleName) : e@Entity(_, _, _) -> page
  	where 	page := ModuleDefs(moduleName, [e], [])
  
  make-valid-page-from-entity(|moduleName) : e@EntityNoSuper(_, _) -> page
  	where 	page := ModuleDefs(moduleName, [e], [])
  	
  entity-to-file(|dirname): e -> e
  	with 	(? Entity(name, _, _) 
     			<+ ?EntityNoSuper(name,_))
  			; <file-exists> $[[<project-path>]/[dirname]]
  			; filename := $[[<project-path>]/[dirname]/[name].app]
	  		; <make-valid-page-from-entity(|$[[dirname]/[name]])
	  		  ; write-ast-to-file(|filename)> e  
	  		    
 generate-main-file(|moduleName): entitynames -> mainAST
   where 	modules := <map(\x -> $[[moduleName]/[x]]\)> entitynames
   			; mainAST := ModuleDefs("entities_generated/main", [modules], [])
rules //entity manipulation

  remove-general-super-from-entity : Entity(name, "Entity",  props) -> EntityNoSuper(name, props)

  complete-entity-with-extends-properties : Entity(name, super, props) -> Entity(name, super, new-props)
  	where  new-props := <get-all-properties-and-functions-from-entityname> name
  	
  get-all-properties-and-functions-from-entityname : name -> props
  	where  props' := <bagof-Properties> name
  		   ; functions' := <bagof-Functions> name
  		   ; props := <concat> [props',functions'] 
  		    