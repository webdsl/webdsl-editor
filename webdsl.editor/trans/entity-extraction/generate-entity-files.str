module entity-extraction/generate-entity-files

imports
	libstratego-lib
	libwebdsl-generator
  	libwebdsl-front
  	util
  	lib/-
  	
rules //builder
	
  generate-entity-files: (selected, position, ast, path, project-path) -> None()
  	where	dirname := "entities_generated"
  			; entities := <all-keys-EntDecl
  				; map(EntDecl; complete-entity-with-extends-properties)> 
  			; filteredEntites := <filter(not(is-builtin-entity))> entities
  			; <create-dir-in-workspace>  dirname 
  			; <map(entity-to-file(|dirname))> filteredEntites
  			; <refresh-workspace-file> project-path  
  	
rules // file stuff  
			      
  make-valid-page-from-entity(|moduleName) : e@Entity(_, _, _) -> page
  	where 	page := ModuleDefs(moduleName, [e], [])
  	
  entity-to-file(|dirname): e@Entity(name,_, props) -> e
  	with 	<file-exists> $[[<project-path>]/[dirname]]
  			; filename := $[[<project-path>]/[dirname]/[name].app]
	  		; <make-valid-page-from-entity(|$[[dirname]/[name]])
	  		  ; write-ast-to-file(|filename)> e  
	  		    
rules //entity manipulation
	
  complete-entity-with-extends-properties : Entity(name, super, props) -> Entity(name, super, new-props)
  	where  new-props := <get-all-properties-and-functions-from-entityname> name
  	
  complete-entity-with-extends-properties : Entity(name, props) -> Entity(name, new-props)
  	where  new-props := <get-all-properties-and-functions-from-entityname> name
  	
  get-all-properties-and-functions-from-entityname : name -> props
  	where  props' := <bagof-Properties> name
  		   ; functions' := <bagof-Functions> name
  		   ; props := <concat> [props',functions'] 