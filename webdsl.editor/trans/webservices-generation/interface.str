module webservices-generation/interface

imports
	fact-extraction/datalog
	libstratego-lib
	libstratego-sglr
	libwebdsl-generator  
  	libwebdsl-front
  	
  	entity-extraction/generate-entity-files
	
	webservices-generation/overlays
	webservices-generation/mapper
	webservices-generation/mobl/mapper
	webservices-generation/mobl/sync-services
	webservices-generation/sync-services
	webservices-generation/related-entities
		 
  	util 
  	lib/- 

overlays
	DirServices = "webservices/services"
rules
	 
  generate-webservices: (selected, position, ast@Application(_, _), path, project-path) -> None()
  	with	<webdsl-editor-init> (path, project-path, ast) //cleanup DR, set appname DR
        	; <full-app-load(|<dirname> path)> ast //not using editor-analyze like other analysis tasks
        	  
  	where	topEntity := <collect-one(\DeriveWebServices(ent, prop)->(ent, prop)\)> ast
  			; rules (TopEntity := topEntity )
  			; <generate-related-entities-functions> project-path
  			; dirname := DirServices()
  			; <create-dir-in-workspace> dirname 
  			; make-mobl-mapper-for-all-entities  
  			; TL := <generate-top-level-webservice>
  			; testservice :=  <generate-test-webservice> 
  			; generate-timestamp-webservice   
  			; generate-mobl-services
  			; generate-sync-services(|<Fst>topEntity)
  			; <generate-edit-mappers> project-path  
  			; modules := <bagof-Service; string-sort; map(\servicename ->Imports( $[[dirname]/[servicename]] )\)> None()
  			; defs := [NativeClassDispatchServletHelper(), <GetDispatchServletHelper>,
  			  		   <generate-available-services-function>, <generate-interface-service>,
  			  		   <generate-add-false-dirty-to-json>, <generate-make-JSON-Error>,
  			  		   <generate-make-JSON-Entity-Error>, <generate-put-ValidationExceptions-in-jsonerrors>]
			; imports := def*
			  |[
			  	imports webservices/related-entities/main 
			  	imports webservices/mappers/main
			  ]|
			; defs' := <concat> [modules,imports, defs]
			; result := <make-valid-page-from-defs(|$[[dirname]/interface])> defs'
			; <write-ast-to-file(|$[[dirname]/interface.app])> result
			  	
  generate-webservices: (selected, position, ast, path, project-path) -> "This builder is only supported on the application file" 	
	where not (<?Application(_, _)> ast)
	where fatal-err-msg(|"This builder is only supported on the application file")
	
rules// generation
	
  generate-available-services-function: x -> function
	where 	services := <bagof-Service>
			; e_services* := <map(\x -> String(x)\)> services
      		; function := def 
      		  |[
      		  	function getAvailableServices() : Set<String> {
      		  	 return {e_services*};
      		  	}
      		  ]|
      		
  generate-interface-service: x -> service
  	where service := def 
	          |[
	          	service webservice ( service : String ) {
	  				if ( service in getAvailableServices() ) {
	    				getDispatchServletHelper().forwardRequest("/webservice_generated_" + service + "/");
	  				} else {
					    var json := JSONObject() ;
					    var errors := JSONArray() ;
					    errors.put("service " + service + " does not exist");
					    json.put("errors", errors);
					    return json;
	  				}
				}
			]|

  generate-service(|name): stat_body -> service
  	where not(<bagof-Service; fetch-elem(?name)> name )
  	where 	rules (Service:+= name)
  			; stat_init := InitService()
  			; stat_exit := <ExitService>
  			; x_servicename :=  $[[ServicePrefix][name]]
   	 		; service  := def 
	          |[
	          	service x_servicename () {
					stat_init
					stat_body
					stat_exit	
				}
			  ]|          	   
   	 			
  generate-test-webservice: x -> service
  	where 	service := <generate-service(|"test")> stat|[var result := "This is a test service";]|  			
  			; service-to-file(|"test") 
  
  generate-timestamp-webservice: x -> service
  	where 	service := <generate-service(|"getTimeStamp")> stat|[var result := now().getTime();]|  			
  			; service-to-file(|"getTimeStamp") 			  
  	 
  generate-top-level-webservice: x -> service
 	where 	x_varname := <new> "ent"
 			; (x_topEntityType, prop) := <TopEntity>
            ; body := webdsl 
              |[
              	var result := JSONArray();
              	for(x_varname : x_topEntityType in  x_topEntityType.all()){
              		result.put(x_varname.toSimpleJSON());
              	}
              ]|  		
            ; service := <generate-service(|"getTopLevelEntities")> body
        	; service-to-file(|"getTopLevelEntities") 
        	     
  service-to-file(|name): service -> service
  	where 	modname := $[[DirServices()]/[name]]
  			; ast := <make-valid-page-from-defs(|modname)>[service]
  			; write-ast-to-file(|$[[modname].app])
  	// where ast 	
  	
rules //general functions
	
  generate-add-false-dirty-to-json: x -> function
	where	function := webdsl |[
		function addDirtyFalse(json : JSONObject) : JSONObject {
			json.put("dirty", "false");
			return json;
		}
	]|
	
  generate-make-JSON-Error: x -> function
	where	function := webdsl |[
		function makeJSONErrorObject(message : String, type : String) : JSONObject {
			var json := JSONObject();
			json.put("message", message);
			json.put("type", type);
			return json;
		}
	]|
	
  generate-make-JSON-Entity-Error: x -> function
	where	function := webdsl |[
		function makeJSONEntityErrorObject(errors : JSONArray, ent : String, id : String) : JSONObject {
			var json := JSONObject();
			json.put("errors", errors);
			json.put("ent", ent);
			json.put("id", id);
			return json;
		}
	]|

  generate-put-ValidationExceptions-in-jsonerrors: x -> function
  	where function := webdsl |[
  		function addValidationExceptionsToLocalErrors(errors : List<NativeValidationException>, localErrors : JSONArray) {
			for(ex : NativeValidationException in errors){
		    	localErrors.put(makeJSONErrorObject(ex.getErrorMessage(), "error"));
			}
		}
		
		function addValidateExceptionsToErrors(exceptions : ValidationExceptionMultiple, errors : JSONArray) {
			for(ex : ValidationException in exceptions.exceptions) {
				errors.put(makeJSONErrorObject(ex.message, "error"));
			}
		}
  	]|
	
	 	   	  	      	