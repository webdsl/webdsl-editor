module webservices-generation/mobl/model-to-mobl

imports
	fact-extraction/datalog
	libstratego-lib
	libstratego-sglr
	libwebdsl-generator
  	libwebdsl-front

  	util 
  	lib/editor-common.generated
  	webservices-generation/util
  	
  	lib/mobl/MoBL
  	lib/mobl/editor-common.generated
  	
  	entity-extraction/generate-entity-pages
  	webservices-generation/overlays
  	webservices-generation/mobl/simple-view  
 
rules 

  generate-mobl-model-builder: (selected, position, ast, path, project-path) -> None()
    where <try(simplify-application-constructor); ?Application(_, _) > ast
  	with <webdsl-editor-init> (path, project-path, ast) //cleanup DR, set appname DR
        ; <full-app-load(|<dirname> path)> ast //not using editor-analyze like other analysis tasks  
        ; <generate-mobl-model> project-path
  	
  generate-mobl-model(): project-path -> project-path
  	with dirname := "webservices/mobl"
  		; <create-dir-in-workspace> dirname
  		; <map(generate-top-level-SyncProperty; generate-top-level-lastSynced-Property)> <all-keys-TopEntity> None()
  		; entities := <all-keys-EntDecl> None()	 
  		; filteredEntities := <filter(not(is-builtin-entity-webservices))> entities
  		; <map(EntDecl; generate-dr-for-highest-parent)> filteredEntities 
  		; completeEntities := <map(generate-extra-properties-for-collections); map(try(generate-extra-type-property)); filter(where(entity-has-no-super)); map(make-entity-with-properties)> filteredEntities
  		; result := <map(entity-to-mobl-entity); make-mobl-model-viewers; make-valid-page-from-mobl-defs(|dirname, "model")> completeEntities
  		; module-to-moblfile(|dirname, "model")
  		; <refresh-workspace-file> $[[project-path]/[dirname]]   
  			  
 generate-mobl-model: (selected, position, ast, path, project-path) -> "This builder is only supported on the application file" 	
	where not (<?ApplicationDefs(_, _, _) <+ ?Application(_, _) > ast)
	with fatal-err-msg(|"This builder is only supported on the application file")
 
rules // file stuff
	
  module-to-moblfile(|dirname, file): mod -> mod
  	with <file-exists> $[[<project-path>]/[dirname]]
  		; filename := $[[<project-path>]/[dirname]/[file].mobl]
	  	; <write-mobl-ast-to-file(|filename)> mod  
	  		  
rules //lookup  			   
  
  make-entity-with-properties: entityname -> EntityNoSuper(entityname, props*)
  	with props_entity* := <concat> [[VersionProperty1()], <get-properties-of-entity-mobl-model> entityname]
  		; props_childeren := <get-subclasses-of-entity; map(get-properties-of-entity-mobl-model); flatten-list> entityname	 
  		; props_childeren* := <map(rename-prop-with-classname)> props_childeren
  		; props* := <uniq> [props_entity*|props_childeren*]	
  
  get-properties-of-entity-mobl-model: entityname -> props'
  	with props := <concat> [<all-properties-with-entity-anno; filter(has-anno(|FromEntity(entityname)))> entityname, 
  			<bagof-ExtraInverseProperty; map(add-anno(|FromEntity(entityname)))> entityname, 
  			<bagof-ExtraProperty; map(add-anno(|FromEntity(entityname)))> entityname]
  		; props' := <filter(where(get-property-name; not(is-restricted-property(|entityname))))> props	
  	//Also only got properties from own entity
  	
  	
  check-searchable-anno: property -> mobl-anno |[searchable]| 
   	where <is-searchable-prop> property  
  		; <get-property-type; ?SimpleSort(type)> property
  		; <fetch-elem(?type)> ToMoblStringTypes()  
  			  
  check-inverse-anno: property ->  mobl-anno |[inverse : x_name ]|
    where(class, name) := <has-valid-inverse-anno> property 
    with  x_name := <get-renamed-prop <+ !name> (class, name) 
    
    
  check-inverse-anno(|class): property -> mobl-anno |[inverse : x_otherpropnew ]| 
  	where not(<has-valid-inverse-anno> property)
    where <get-property-type; ?GenericSort(_,_)> property
   		; (otherEnity, x_otherprop) :=  <has-valid-counter-inverse-anno> property
    with  x_otherpropnew :=  <get-renamed-prop <+ !x_otherprop> (otherEnity, x_otherprop) 
  			
  check-inverse-anno(|class): property ->  anno
  	where not(<has-valid-inverse-anno> property)
    where <get-property-type; ?GenericSort(_,_)> property
    with propertyname := <get-property-name> property
    	; propertyname' := <try(get-original-propname-name)> 
   		; anno := <ExtraInverseAnno> (class, propertyname')
    where not(<has-valid-counter-inverse-anno> property)
  
  has-valid-inverse-anno: property -> (class, name)
  	where <get-property-annos; fetch(?InverseAnno(class, name))> property
  	where not (<RestrictedProperty>(class,name))
  
  has-valid-counter-inverse-anno: property -> (otherEnity, x_otherprop)
	with propertyname := <get-property-name> property
		; propertyname' := <try(get-original-propname-name)>  
    	; class' := <(get-anno(?FromEntity(x)); !x) > property 
  	where (otherEnity, x_otherprop) := <InverseProperty> (class', propertyname')
    where not (<RestrictedProperty> (otherEnity, x_otherprop) )
    
rules //rename(subclasses stuf)
	
  rename-prop-by-dr: Property(name, k, str, annos){anno*} -> Property(newname, k, str, annos){anno*}
	with class := <fetch-elem(?FromEntity(x)); !x> anno* 
		; newname := <get-renamed-prop <+ !name> (class, name) 

  rename-prop-by-dr: DerivedProperty(name, k, str, annos, derive){anno*} -> DerivedProperty(newname, k, str, annos, derive){anno*}
	with class := <fetch-elem(?FromEntity(x)); !x> anno* 
		; newname := <get-renamed-prop <+ !name> (class, name) 

  rename-prop-with-classname: Property(name, k, str,annos){anno*} -> Property(newname, k, str,annos){anno*}
	where not(<string-starts-with(|"generatedPropertyForInverse_")> name)
	with <fetch-elem(?FromEntity(x))> anno*
		; newname := $[[x]_[name]] 
		; rules(
			get-renamed-prop :+ (x, name) -> newname
		 	get-original-propname-name :+ newname -> name
		)

  rename-prop-with-classname: DerivedProperty(name, k, str,annos, derive){anno*} -> DerivedProperty(newname, k, str,annos, derive){anno*}
	where not(<string-starts-with(|"generatedPropertyForInverse_")> name)
	with <fetch-elem(?FromEntity(x))> anno*
		; newname := $[[x]_[name]] 
		; rules(
			get-renamed-prop :+ (x, name) -> newname
			get-original-propname-name :+ newname -> name
		)

  rename-prop-with-classname: prop{anno*} -> prop{anno*}
	where name := <get-property-name> prop
		; (<string-starts-with(|"generatedPropertyForInverse_")> name)
	with <fetch-elem(?FromEntity(x))> anno*
		; rules(
			get-renamed-prop :+ (x, name) -> name
			get-original-propname-name :+ name -> name
		) 
				  
rules //generation 
	
  generate-extra-properties-for-collections: entityname -> entityname
	with props := <all-properties-with-entity-anno> entityname
		; propswithoutinvers := <filter(has-anno(|FromEntity(entityname));generic-type-has-no-inverse-anno(|entityname); not(is-restricted-property(|entityname))) >
		; map(declare-extra-prop(|entityname))

 
  is-restricted-property(|class): property -> property
  	where <RestrictedProperty> (class, <get-property-name> property) 
 
  generic-type-has-no-inverse-anno(|class): property ->  property
  	where not (<has-valid-inverse-anno> property)
    where <get-property-type; ?GenericSort(_,_)> property
    	; propname := <get-property-name> property
    where not (<InverseProperty> (class, propname))
  
  generic-type-has-no-inverse-anno(|class): property ->  property
  	where not (<has-valid-inverse-anno> property)
    where <get-property-type; ?GenericSort(_,_)> property
    	; propname := <get-property-name> property
		; (class', propname') := <InverseProperty> (class, propname)
		; <RestrictedProperty> (class',propname')  
   
  declare-extra-prop(|class): property -> property
  	with entity := <get-property-type; get-inner-type-name> property
  		; propertyname := <get-property-name> property
  		; newProperty := <generate-inverse-property(|class)> property
  		; x_newPropertyName := <get-property-name> newProperty 
  		; parent := <find-highest-parent>class 
  		; rules(
  			ExtraInverseProperty:+ entity -> newProperty
  			ExtraInverseAnno: (parent, propertyname) -> mobl-anno |[inverse : x_newPropertyName ]|
  		)
  
  generate-extra-type-property: entityname -> entityname
  	where  <entity-has-no-super> entityname
  		; <SubClass> entityname
  	with property := Property("typeField", Simple(), SimpleSort("String"), [])
   		; rules( ExtraProperty:+ entityname -> property)
  		
  generate-top-level-SyncProperty: entityname -> entityname
   	with property := Property("sync", Simple(), SimpleSort("Bool"), [])
   		; entityname':= <find-highest-parent> entityname
   		; rules( ExtraProperty:+ entityname' -> property) 
   
  generate-top-level-lastSynced-Property: entityname -> entityname
   	with property := Property("lastSynced", Simple(), SimpleSort("Int"), [])
   		; entityname':= <find-highest-parent> entityname
   		; rules( ExtraProperty:+ entityname' -> property) 		  
   
  generate-inverse-property(|class): property ->  Property(name, Ref() , GenericSort("List", [SimpleSort(class)]), [InverseAnno(class, propertyname)])
    where proptype := <get-property-type; ?GenericSort("List", _)> property
    with propertyname := <get-property-name> property
    	; name := $[generatedPropertyForInverse_[class]_[propertyname]]    
   
  generate-inverse-property(|class): property -> Property(name, Ref() , SimpleSort(class), [])
    where proptype := <get-property-type; ?GenericSort("Set", _)> property
    with propertyname := <get-property-name> property
    	; name := $[generatedPropertyForInverse_[class]_[propertyname]]  	
    	  	   
rules //transform 

  entity-to-mobl-entity: EntityNoSuper(x_name, props) -> moblEntity
  	with cleanedProps  := <map(try(remove-derive-from-property))> props
  		; ebd_props* := <filter(property-to-mobl-property(|x_name))>
 		; moblEntity := mobl-def 
 			|[
 				entity x_name {
 			  		ebd_props*
 			  	}	
 			]|
 			  
  property-to-mobl-property(|class): p@Property(x_name, sort, type, annos) -> result
  	where t_newType := <type-to-mobl-type> type
    with if (a_newannos := <annos-to-mobl-annos(|class)> p) 
  			then  result := mobl-entBodyElem |[x_name : t_newType (a_newannos) ]| 
  			else  result := mobl-entBodyElem |[x_name : t_newType ]| 
  		end

  annos-to-mobl-annos(|class): property -> result
  	with anno* := [<check-searchable-anno <+ !None()> property|[]]
  		; anno'' := [< check-inverse-anno <+ check-inverse-anno(|class) <+ !None()> property|anno*]
  		; result := <filter(not(?None()))>
  	where <length; int-gt(|0)> result
    
rules // type transformations  	
  
  type-to-mobl-type: SimpleSort(type) -> mobl-t|[String]| 
   	where 	<fetch(?type)> ToMoblStringTypes
  
  type-to-mobl-type: SimpleSort(type) -> mobl-t|[Num]|
   	where 	<fetch(?type)> ToMoblNumTypes

  type-to-mobl-type: SimpleSort("Bool") -> mobl-t|[Bool]| 
 
  type-to-mobl-type: SimpleSort(type) -> mobl-t|[DateTime]|
  	where 	<fetch(?type)> MoblDateTimeTypes 

  type-to-mobl-type: SimpleSort(x_type) -> mobl-t|[x_newType]|
   	where 	x_newType := <IsEntity; find-highest-parent> x_type
   	where not (<is-builtin-entity-webservices> x_type)
  
  type-to-mobl-type: GenericSort(_, type) -> mobl-t|[Collection<t_paramtype>]| 
   	where	<Hd; is-entity-type> type
   		 	; t_paramtype := <Hd; type-to-mobl-type> type 