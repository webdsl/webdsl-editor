module webservices-generation/mobl/sync-services
imports 
	  	lib/mobl/MoBL
  		lib/mobl/editor-common.generated 
  		
  		webservices-generation/mobl/model-to-mobl
  		webservices-generation/mobl/unsynced-functions
     	webservices-generation/model-tree
     	webservices-generation/util

  	  	
  	  	util 
	  	lib/-
rules 
	
  generate-mobl-services: x -> x
	where  dirname := "webservices/mobl"
  			; <create-dir-in-workspace> dirname
  			; generate-mobl-unsynced-functions  
  			; generate-mobl-top-level-service
  			; entities :=  <all-keys-EntDecl; filter(not(is-builtin-entity); where(entity-has-no-super))> None()	// TODO : flatten - removes subentities
  			; <map(generate-mobl-sync-service)> entities
  			; generate-mobl-timestamp-service 
  			; generate-mobl-edit-service   
  			; syncfunctions := <map(generate-mobl-sync-functions)> entities  
  			; totalsyncfunction := <generate-mobl-total-sync-function>
  			; editsyncfunction := <generate-mobl-edit-sync-function>  entities  
  			; clearEditFunction := <generate-mobl-clear-edit-function>  entities 
  			; restoreFunction := <generate-mobl-restore-object-function> entities
  			; service := <bundle-services> None()
  			; imports :=  mobl-def* 
  			  |[
  			  	import webservices::mobl::mapper
  			  	import webservices::mobl::model
  			  	import webservices::mobl::unsync
  			  	import webservices::mobl::simpleView
  			  ]|  
  			; result := <concat> [imports, service, syncfunctions, [totalsyncfunction, editsyncfunction, clearEditFunction, restoreFunction, 
  			  						<generate-mobl-get-listof-Ids-function>, <generate-mobl-sync-integration-functions>]]
  			; <make-valid-page-from-mobl-defs(|dirname, "sync")> result
  			; module-to-moblfile(|dirname, "sync")
  			; <refresh-workspace-file> $[[<project-path>]/[dirname]]   
	 

  bundle-services: x -> result
 	where 	sve_resources* := <bagof-ServiceMobl>None()
			; result := mobl-def* 
			  |[
			  	  service Sync {
			  	  	sve_resources*
			  	  }
			  ]|
			  	
  generate-mobl-top-level-service: x -> x
  	where	(name,prop) := <TopEntity> 	
          	; x_mapper := $[[name]Mapper]	
          	; service := mobl-serviceElem 
          	  |[
          	  	resource getTopLevelEntities(): void{
          	  		uri = "webservice/getTopLevelEntities"
     				method = "PUT"
      				data = {}
      				mapper = x_mapper
          	  	}
          	  ]|	
          	; rules (ServiceMobl:+= service)
  
  generate-mobl-sync-service: entity -> entity
  	where  	e_uri := $["webservice/sync[entity]"]
          	; x_servicename := $[sync[entity]]
          	; x_mapper := $[[entity]Mapper]		
           	; service := mobl-serviceElem 
          	  |[
          	  	resource  x_servicename (arg : JSON): void{
          	  		uri = e_uri
     				method = "PUT"
      				data = JSON.stringify(arg)
      				mapper = x_mapper
          	  	}
          	  ]|
           ; rules (ServiceMobl:+= service)
 
rules //timestamp service 
	
  generate-mobl-timestamp-service: x -> x
  	where  	service := mobl-serviceElem 
          	  |[
          	  	resource  getTimeStamp (): Num{
          	  		uri = "webservice/getTimeStamp"
     				method = "PUT"
      				mapper = timeStampMapper
          	  	}
          	  ]|
           ; rules (ServiceMobl:+= service)	
  
rules // editService
	
  generate-mobl-edit-service: x -> x
  	where  	service := mobl-serviceElem 
          	  |[
          	  	resource  sendEdits (arg : [JSON]): JSON {
          	  		uri = "webservice/syncDirtyObjects"
     				method = "PUT"
     				data = JSON.stringify(arg)
      				mapper = errorMapper
          	  	}
          	  ]|
           ; rules (ServiceMobl:+= service)	
  
  generate-mobl-edit-sync-function: entities -> function  
    where	stat_objectstatement* := <map(generate-sync-edit-mobl-statements)> entities			
 			; function := mobl-def 
 			  |[
 			  	 function syncEdits() : JSON {
 			  	 	setSyncFlag(true);
   				 	var array : [JSON] = [];
    				stat_objectstatement*
    				var errors = Sync.sendEdits(array);
    				setSyncFlag(false);
    				return errors;
 				}
 			  ]|    
 			   
  generate-sync-edit-mobl-statements: x_entity -> statements
    where 	x_topEntity := <TopEntity; Fst>
 			; <?x_topEntity> x_entity
    		; x_entityname := $["[x_entity]"]
    		; statements := mobl-stat*
   				|[
					var value = x_entity.all().filter("sync", "=",true).filter("dirty", "=", true).selectJSON(["*"]);	
					var json = Dynamic(name=x_entityname, value=value);
					array.push(json);
   				]|
  
  generate-sync-edit-mobl-statements: x_entity -> statements
    where 	x_topEntity := <TopEntity; Fst>
 	where not( <?x_topEntity> x_entity )
    where	x_entityname := $["[x_entity]"]
    		; statements := mobl-stat*
   				|[
					var value = x_entity.all().filter("dirty", "=", true).selectJSON(["*"]);	
					var json = Dynamic(name=x_entityname, value=value);
					array.push(json);
   				]|				        

	            
rules // generation of sync-functions
	 
 generate-mobl-sync-functions: entity -> function
 	where	stat_objectstatement := <get-all-mobl-objects-of-entity-statement>			
 			; x_functionname := $[syncAll[entity]]
 			; e_logstring := $["time needed for syncing [entity]: "]
 			; function := mobl-def 
 			  |[
 			  	 function x_functionname() {
   				 	var begin = now();
    				stat_objectstatement
    				log(e_logstring + ( ( now() - begin ) / 1000 ).toString());
 				}
 			  ]|
 			  
 get-all-mobl-objects-of-entity-statement: x_entity -> statement
 	where x_topEntity := <TopEntity; Fst>
 	where not( <?x_topEntity> x_entity )
 	where	x_servicename := $[sync[x_entity]]
          	; statement := mobl-stat |[ Sync.x_servicename(x_topEntity.all().filter("sync", "=", true).selectJSON(["id", "lastSynced"]));]|
          	  				  
 
 get-all-mobl-objects-of-entity-statement: x_entity -> statement
 	where	x_topEntity := <TopEntity; Fst>
 			; <?x_topEntity> x_entity
 			; x_servicename := $[sync[x_entity]]
			; statement := mobl-stat |[ Sync.x_servicename(x_topEntity.all().filter("sync", "=", true).selectJSON(["id", "lastSynced"]));]|
	
 generate-mobl-total-sync-function:	x -> function
 	where 	stat_calls* := <Fst; generate-list-sync-calls> <TopEntity>
   			; function := mobl-def 
   			  |[
   			  	function syncAll() {
   			  		setSyncFlag(true);
   			  		var begin = now();
   			  		stat_calls*
   			  		log("totaltime:" + ((now() - begin) / 1000).toString());
   			  		setSyncFlag(false);
   			  	}
   			  ]|
   			   
  generate-list-sync-calls: x_topEntity -> list
 	where 	orderedentities := <make-node-of-entity(|[]); bf-collect(is-string); map(find-highest-parent) ;uniq> x_topEntity // TODO it is not clear of order mathers at the moment so keep this but flatten it
 			; stat_firstsyncs* :=<map(make-sync-call)> orderedentities
 			; list := mobl-stat* 
 			  |[
 			  	var time = Sync.getTimeStamp();
 			  	stat_firstsyncs*
 			  	foreach(tl in x_topEntity.all().filter("sync", "=", true) ) {
 			  		tl.lastSynced = time;
 			  	}
 			  ]|
 			  
  make-sync-call: entityname -> call
 	where x_servicename := $[syncAll[entityname]]
 		  ; call := mobl-stat |[x_servicename();]|

rules// clear function 
  generate-mobl-clear-edit-function: entities -> function
  	where 	stat_clear* := <map(generate-clear-edit-statement)>
  			; function := mobl-def 
  			|[
				function clearDirty (excludes :  [String]) {
					stat_clear*	
				}  		
  			]|
  			
  generate-clear-edit-statement: x_entity -> statement
  	where	x_topEntity := <TopEntity; Fst>
 			; <?x_topEntity> x_entity
 			; statement := mobl-stat* 
 			  |[
 			  		var value = x_entity . all ( ) . filter ( "sync" , "=" , true ) . filter ( "dirty" , "=" , true );
				  	foreach(ent in value){
				  		if(!excludes.contains(ent.id)){
				  			ent.dirty = false;
				  		}
				  	}
 			  ]|
  
  generate-clear-edit-statement: x_entity -> statement
  	where	x_topEntity := <TopEntity; Fst>
 	where not(<?x_topEntity> x_entity)
 	where	statement := mobl-stat* 
 			  |[
 			  		var value = x_entity . all ( ). filter ( "dirty" , "=" , true );
				  	foreach(ent in value){
				  		if(!excludes.contains(ent.id)){
				  			ent.dirty = false;
				  		}
				  	}
 			  ]|
 			  
rules// get list of id's from errors
	  			  
   generate-mobl-get-listof-Ids-function: x -> function
  	where 	function := mobl-def 
  			|[
				function getListofIds (errorents :  JSON) : [String] {
					var ids : [String] = [];
					foreach (errorent in errorents) {
						if(errorent.id){
							ids.push(errorent.id);	
						}	
					}
					return ids;	
				}  		
  			]|  

rules//some sync integration 
	
  generate-mobl-sync-integration-functions : x -> defs
  	where  defs := mobl-def* 
  			|[
  				function testSync( errorCTRL : Control3<JSON, ?,Function1<JSON , void>> = showErrors, detailCTRL : Control2<String, String> = showObject) : void {
					var errors = syncEdits();
					clearDirty(getListofIds(errors));
					if(errors.length == 0) {
						continueSync();
					} else {
						showErrorsEmptyScreen(errors, errorCTRL, detailCTRL, ingnoreAndContinue);
					}	
				}
				
			    function ingnoreAndContinue(jsonArray : JSON) {
				  	foreach(error in jsonArray) {
				  			if(error.restore) {
				  				restoreObject(error.ent, error.restore);
				  			}	
				  	}
				  	continueSync();
				  }
				  
				  function continueSync ( ) {
				    clearDirty([]);
				    syncAll();
				  }
  			]|		

  generate-mobl-restore-object-function: entities -> function
  	where 	stat_ifs := <map(generate-mobl-restore-single-object-from-JSON)>entities
  			; function := mobl-def	
  			|[
  				function restoreObject ( type : String , ent : JSON ) {
				  	stat_ifs
				}
  			]|
  generate-mobl-restore-single-object-from-JSON: x_entity -> statement
  	where  	x_entityString := String(x_entity)
  			; statement :=mobl-stat* 
  			|[
  				if ( type == x_entityString ) {
				      x_entity . fromSelectJSON ( ent );
				}
  			]|	
   