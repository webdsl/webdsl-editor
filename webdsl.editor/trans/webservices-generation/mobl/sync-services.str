module webservices-generation/mobl/sync-services
imports 
	  	lib/mobl/MoBL
  		lib/mobl/editor-common.generated 
  		
  		webservices-generation/mobl/model-to-mobl
  		webservices-generation/mobl/unsynced-functions
     	webservices-generation/model-tree
  	  	
  	  	util 
	  	lib/-
rules 
	
  generate-mobl-services: x -> x
	where  dirname := "webservices/mobl"
  			; <create-dir-in-workspace> dirname
  			; generate-mobl-unsynced-functions  
  			; generate-mobl-top-level-service
  			; entities :=  <all-keys-EntDecl; filter(not(is-builtin-entity))> None()	
  			; <map(generate-mobl-sync-service)> entities
  			; generate-mobl-timestamp-service  
  			; syncfunctions := <map(generate-mobl-sync-functions)> entities  
  			; totalsyncfunction := <generate-mobl-total-sync-function>  
  			; service := <bundle-services> None()
  			; imports :=  mobl-def* 
  			  |[
  			  	import webservices::mobl::mapper
  			  	import webservices::mobl::model
  			  	import webservices::mobl::unsync
  			  ]|  
  			; result := <concat> [imports, service, syncfunctions, [totalsyncfunction]]
  			; <make-valid-page-from-mobl-defs(|dirname, "sync")> result
  			; module-to-moblfile(|dirname, "sync")
  			; <refresh-workspace-file> $[[<project-path>]/[dirname]]   
	 

  bundle-services: x -> result
 	where 	sve_resources* := <bagof-ServiceMobl>None()
			; result := mobl-def* 
			  |[
			  	  service Sync {
			  	  	sve_resources*
			  	  }
			  ]|
			  	
  generate-mobl-top-level-service: x -> x
  	where	(name,prop) := <TopEntity> 	
          	; x_mapper := $[[name]Mapper]	
          	; service := mobl-serviceElem 
          	  |[
          	  	resource getTopLevelEntities(): void{
          	  		uri = "webservice/getTopLevelEntities"
     				method = "PUT"
      				data = {}
      				mapper = x_mapper
          	  	}
          	  ]|	
          	; rules (ServiceMobl:+= service)
  
  generate-mobl-sync-service: entity -> entity
  	where  	e_uri := $["webservice/sync[entity]"]
          	; x_servicename := $[sync[entity]]
          	; x_mapper := $[[entity]Mapper]		
           	; service := mobl-serviceElem 
          	  |[
          	  	resource  x_servicename (arg : JSON): void{
          	  		uri = e_uri
     				method = "PUT"
      				data = JSON.stringify(arg)
      				mapper = x_mapper
          	  	}
          	  ]|
           ; rules (ServiceMobl:+= service)
 
rules //timestamp service 
	
  generate-mobl-timestamp-service: x -> x
  	where  	service := mobl-serviceElem 
          	  |[
          	  	resource  getTimeStamp (): Num{
          	  		uri = "webservice/getTimeStamp"
     				method = "PUT"
      				mapper = timeStampMapper
          	  	}
          	  ]|
           ; rules (ServiceMobl:+= service)	
            
rules // generation of sync-functions
	 
 generate-mobl-sync-functions: entity -> function
 	where	stat_objectstatement := <get-all-mobl-objects-of-entity-statement>			
 			; x_functionname := $[syncAll[entity]]
 			; e_logstring := $["time needed for syncing [entity]: "]
 			; function := mobl-def 
 			  |[
 			  	 function x_functionname() {
   				 	var begin = now();
    				stat_objectstatement
    				log(e_logstring + ( ( now() - begin ) / 1000 ).toString());
 				}
 			  ]|
 get-all-mobl-objects-of-entity-statement: x_entity -> statement
 	where x_topEntity := <TopEntity; Fst>
 	where not( <?x_topEntity> x_entity )
 	where	x_servicename := $[sync[x_entity]]
          	; statement := mobl-stat |[ Sync.x_servicename(x_topEntity.all().filter("sync", "=", true).selectJSON(["id"]));]|
          	  				  
 
 get-all-mobl-objects-of-entity-statement: x_entity -> statement
 	where	x_topEntity := <TopEntity; Fst>
 			; <?x_topEntity> x_entity
 			; x_servicename := $[sync[x_entity]]
			; statement := mobl-stat |[ Sync.x_servicename(x_topEntity.all().filter("sync", "=", true).selectJSON(["id"]));]|
	
 generate-mobl-total-sync-function:	x -> function
 	where 	stat_calls* := <Fst; generate-list-sync-calls> <TopEntity>
   			; function := mobl-def 
   			  |[
   			  	function syncAll() {
   			  		var begin = now();
   			  		stat_calls*
   			  		log("totaltime:" + ((now() - begin) / 1000).toString());
   			  	}
   			  ]|
   			  
 generate-list-sync-calls: TopEntity -> list
 	where 	orderedentities := <make-node-of-entity(|[]); bf-collect(is-string); uniq> TopEntity
 			; stat_syncunsynced* := <map(make-unsynced-check)> orderedentities
 			; stat_firstsyncs* :=<map(make-sync-call)> orderedentities
 			; list := mobl-stat* 
 			  |[
 			  	stat_firstsyncs*
 			  	var allsynced = false;
 			  	while(!allsynced) {
 			  		allsynced = true;
 			  		stat_syncunsynced*
 			  	}
 			  ]|
 			  
 make-sync-call: entityname -> call
 	where x_servicename := $[syncAll[entityname]]
 		  ; call := mobl-stat |[x_servicename();]|
 		    
 make-unsynced-check: entityname -> check 
 	where 	x_nrunsynced := $[nrUnsynced[entityname]]
 			; x_unsynced := $[syncUnsynced[entityname]]
 	 	  	; check := mobl-stat 
 	 	  	  |[
 	 	  	  	if(x_nrunsynced()) {
 	 	  	  		x_unsynced();
 	 	  	  		allsynced = false;
 	 	  	  	}
 	 	  	  ]|