module caching

imports
    parse
    ../lib/editor-common.generated
    libwebdsl-front
    util
    webdsl_editor

strategies  
  
  store-def-to-mod-desugar = preserve(desugar-all; store-def-to-mod)
  store-def-to-mod-desugar(|ent) = desugar-all; add-anno(|FromEntity(ent)); store-def-to-mod
        
  store-def-to-mod: def -> def
    where mod := <CurrentFile>
        ; rules(DefToModule :+ def -> mod)
    
strategies // storing for compiler typechecker
  
  // Already stored
  store-def: def -> def
    where mod := <CurrentFile>
        ; <ReadingFromCache> mod
 
  store-def: def -> def
    where mod := <CurrentFile>
        ; rules(ModuleDefs :+ mod -> def)
  
  // If CurrentFile is not set, we're parsing the main file
  store-def = id

signature constructors
  
  // Wrap int in ModificationTerm to avoid LazyTerm bug
  ModificationTime : Term -> Term

strategies

  check-editor-version: cache-path -> cache-path
// NATHAN: disable until Spoofax/92 is solved. [http://yellowgrass.org/issue/Spoofax/92]
    with  ModificationTime(editor-ts) := <import-term(editor.timestamp.aterm)>
//    with editor-ts := 1271360674
    where <gt <+ editor-new-version-once; fail> (<modification-time> cache-path, editor-ts)
    
  editor-new-version-once =
    try(
        where(not(EditorNewVersionShowed))
      ; rules(EditorNewVersionShowed := True())
      ; debug(!"Editor is updated; refreshing cache.")
    )

  /* Custom version of open-import */
  open-import-sig(resolve-path, parse-file, record-declarations, record-sigs):
    import -> import
    where
      if not(!import => COMPLETION(_)) then
        path       := <resolve-path> import;
        cache-path := <import-cache-path> path;
        {| CurrentFile:
          rules(CurrentFile := path);
          if not(<IsImported> path) then
	          rules(
	            IsImported: path
	          ); 
	          if   <is-newer> (cache-path, path); <check-editor-version> cache-path then
	            file := <log-time-helper(ReadFromFile | "read")> cache-path;
	            <record-sigs> file 
	          else
	            where(!cache-path; debug(!"Updating cache: "));
	            file := <parse-file> path;
	            sigs := <record-declarations> file;
	            if <file-exists> path then
	              // Only cache if on filesystem (e.g., ignore libstratego-lib)
	              <WriteToBinaryFile> (cache-path, sigs)
	            end
	          end
	        end
        |}
      end
      
strategies

  fix-module-name(|fullpath): Module(modname, sections) -> Module(newname, sections)
    with  base := <concat-strings> [<ApplicationIniDir <+ prim("SSL_EXT_projectpath")>, "/"]
    where (l,r) := <split-after> (fullpath, base)
        ; not(<eq> (r, "")) // base not found
        ; newname := <split-before; Fst> (r, ".app")
        ; not(<eq> (newname, modname))  // only succeed if actually renamed
        ; <concat-strings; debug> ["Renaming module '",modname,"' to '",newname,"'."]
        
strategies // bench
  
   dbg-time(s|msg) =
      where(starttime := <times>)
    ; s
    ; where(
          total := <diff-times> (<times>,starttime)
        ; t1 := <self-children-user-time; ticks-to-seconds; real-to-string(|3)> total
        ; str := <concat-strings> [msg, ": ", t1, " s"]
        ; debug
      )