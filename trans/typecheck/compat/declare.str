module typecheck/compat/declare

imports
    libwebdsl-front
    libwebdsl-generator
    caching
    
rules
  
  declare-global = 
      is-global-decl-all
    ; where(store-tc-def)
    ; declare-all // also property, funcs
      
  declare-global = 
      is-global-decl
    ; where(store-tc-def)
    ; declare
      
  is-global-decl =
      ?Enum(_,_)
    <+?Function(_,_,_,_)
    <+?FunctionNoReturn(_,_,_)
    <+?AccessControlPrincipal(_,_)    // for securityContext
    <+?Define(_,_,_,_,_)
    
  is-global-decl-all =
      where(get-entity-def)
    <+?ExtendEntity(_,_)
    
  // special
  declare-global =
      ?Predicate(_,_,_)
    ; where(store-tc-def)
    ; declare-global-pred
    
rules // only global if defined on top level
  
  declare-global =
      ?GlobalsDefinition(elems)
    ; <filter(try(declare-global-toplevel))> elems
  
  declare-global =
      (?Section(_,elems) <+ ?AccessControlDefinition(_,elems))
    ; <filter(try(declare-global); try(declare-global-toplevel))> elems
    ; fail // allow further traversal (like imports)
  
  is-global-toplevel =
  	 ?VarDeclInit(_,_,_)
   <+?VarDeclInitInferred(_,_)
  
  declare-global-toplevel =
      is-global-toplevel
    ; where(store-tc-def)
    ; declare
      