module entities

imports
  libstratego-lib
  libwebdsl-front
  webdsl
      
signature
  constructors
    Ent : Term * Term * Term * Term -> Ent      
    
rules // types

  declaration-of : SimpleSort(x) -> <EntDecl> x
    
rules

  desugar-def : EntityNoSuper(name, body) -> Ent(name,None(),body,None())    
  desugar-def : Entity(name, super, body) -> Ent(name,super,body,None())    
  desugar-def : SessionEntity(name,body) -> Ent(name,None(),body,"session")
    
rules // declaring entities

  declare-def:
    ent@Ent(x,_,_,_) -> ent
    with rules(EntDecl : x -> ent)
       ; <store-imported-def> ent

//  name-of : EntityNoSuper(x, prop*) -> x
//  type-of : EntityNoSuper(x, prop*) -> SimpleSort(x)

rules 

  check :
    Ent(x, _,_,_) -> (x, $[Entity names must start with a capital letter])
    where require(<explode-string; Hd; is-upper> x) 
     
  check :
    ent@Ent(x,_,_,_) -> (x, $[Entity '[x]' defined more than once])
    where require(<EntDecl> x; ?ent)
        ; !ent; debug(!"Decl 1: ")
        ; <EntDecl> x; debug(!"Decl 2: ")
