module typecheck/declare

imports
    libwebdsl-front
    libwebdsl-generator
    caching
    
rules // global declares
  
  declare-global = 
      is-global-decl
    ; where(store-tc-def; store-def-to-mod-desugar)
    ; declare
      
  is-global-decl =
      ?Enum(_,_)
    <+?AccessControlPrincipal(_,_)    // for securityContext
    <+?Define(_,_,_,_,_)
    <+?Predicate(_,_,_)
    <+?Derive(_,_,_)                  // declares page names 
    <+?DeriveCrud(_)
  
rules // global declares with subelements

  declare-global = 
      where(get-entity-def => (name,body))
    ; declare
    ; where(<map(store-def-to-mod-desugar(|name))> body)
    ; store-tc-def
    ; store-def-to-mod-desugar
    
  declare-global =
      ?ExtendEntity(ent, body)
    ; declare
    ; where(<map(store-def-to-mod-desugar(|ent))> body)
    ; store-tc-def; store-def-to-mod-desugar
      
rules // only global if defined on top level
  
  declare-global =
      ?GlobalsDefinition(elems)
    ; <filter(try(declare-global-toplevel))> elems
  
  declare-global =
      (?Section(_,elems) <+ ?AccessControlDefinition(_,elems))
    ; <filter(try(declare-global-toplevel))> elems
    ; fail // allow further traversal (like imports)
  
  is-global-toplevel =
  	 ?VarDeclInit(_,_,_)
   <+?VarDeclInitInferred(_,_)
   <+?Function(_,_,_,_)
   <+?FunctionNoReturn(_,_,_)
    
  declare-global-toplevel =
      is-global-toplevel
    ; where(store-tc-def; store-def-to-mod-desugar)   // store VarDecl -> mod
    ; declare
    